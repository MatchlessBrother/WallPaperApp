apply plugin: 'com.android.application'

def keyStorePropertiesFile = rootProject.file("keystore.properties")
def dev_keyStorePropertiesFile = rootProject.file("devkeystore.properties")
def keyStoreProperties = new Properties()
def dev_keyStoreProperties = new Properties()
keyStoreProperties.load(new FileInputStream(keyStorePropertiesFile))
dev_keyStoreProperties.load(new FileInputStream(dev_keyStorePropertiesFile))

android{
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig{
        resConfigs "xhdpi"
        ndk{abiFilters "armeabi"}
        flavorDimensions "default"
        vectorDrawables.useSupportLibrary = true
        applicationId "company.petrifaction.boss"
        versionCode  rootProject.ext.versionCode
        versionName  rootProject.ext.versionName
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        resConfigs "zh-rCN","uk","pt","pl","it","fr","es","en","de"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions{
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    productFlavors{
        /**正式版**/
        publish{
            manifestPlaceholders = [APP_NAME:"@string/app_name"]
            buildConfigField 'String', "SERVICE_URL",'"http://47.92.170.43"'
        }

        /**测试版**/
        develop{
            manifestPlaceholders =[APP_NAME:"@string/app_name_dev"]
            buildConfigField 'String', "SERVICE_URL",'"http://47.92.170.43"'
        }
    }

    signingConfigs{
        release{
            keyAlias keyStoreProperties['KeyAlias']
            storeFile file(keyStoreProperties['StoreFile'])
            keyPassword keyStoreProperties['KeyPassword']
            storePassword keyStoreProperties['StorePassword']
        }

        debug{
            keyAlias dev_keyStoreProperties['KeyAlias']
            storeFile file(dev_keyStoreProperties['StoreFile'])
            keyPassword dev_keyStoreProperties['KeyPassword']
            storePassword dev_keyStoreProperties['StorePassword']
        }
    }

    buildTypes{
        release{
            debuggable false
            jniDebuggable false
            minifyEnabled true
            multiDexEnabled true
            zipAlignEnabled true
            shrinkResources true
            /**配置签名密匙*/
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug{
            debuggable true
            jniDebuggable true
            minifyEnabled false
            multiDexEnabled true
            zipAlignEnabled true
            shrinkResources false
            /**不用配置签名密匙*/
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /**加快dex速度*/
    dexOptions{
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "2048m"
    }
}

dependencies{
    implementation project(':DevLibrary')
    testImplementation 'junit:junit:4.12'
    implementation 'me.jessyan:autosize:1.1.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'cn.bingoogolapple:bga-banner:2.2.5@aar'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'jp.wasabeef:glide-transformations:4.0.1'
    implementation 'com.flyco.dialog:FlycoDialog_Lib:1.3.2@aar'
    implementation 'com.gyf.immersionbar:immersionbar:2.3.3-beta15'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    implementation ('com.github.hotchemi:permissionsdispatcher:3.3.1')
    {   exclude group: 'com.android.support', module: 'support-v13'  }
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.3.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}